 msg←Import(ns dir);opts;container;isChild;nsref;inFail;numFail
 :If ~×⎕NC'⎕SE.Link.DEBUG'
     ⎕SE.Link.DEBUG←0
 :ElseIf ⎕SE.Link.DEBUG=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap ⎕SE.Link.DEBUG↓0
     opts←⎕NS ⍬
     opts.watch←'none'
     opts.source←'dir'

     container←⊃⌽2⍴⎕RSI↓⍨⎕XSI(∊⍨×¯1+⍳)⊂'⎕SE.UCMD'    ⍝ ns is relative to where we were called from
     ns←⍕ns
     isChild←~'⎕SE'(,'#')'⎕DMX'∊⍨⊂ns
     ns,⍨←isChild/'.',⍨⍕container
     nsref←⍎ns container.⎕NS ⍬

     win←'Windows'≡7↑⊃'.'⎕WG'APLVersion'
     OsSlash←'\'@{⍵='/'}⍣win
     dir←OsSlash dir

     inFail←⊃opts FixFiles nsref dir
     msg←'Imported: ',ns,' ← ',dir
     numFail←≢inFail
     :If ×numFail
         msg,←'; ',(⍕numFail),' failed: ',1↓¯1↓⎕JSON inFail
     :EndIf
 :Else
     ⎕SIGNAL⊂⎕DMX.(('EN'EN)('Message'Message))
 :EndTrap
