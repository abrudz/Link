 msg←Break nss
 ;ns;linked;broken;missing;mask;⎕IO;there
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :If ×⎕NC'⎕SE.Link.Links'
     :AndIf ×≢⎕SE.Link.Links
         ⎕IO←1
         there←⊃⌽2⍴⎕RSI↓⍨⎕XSI(∊⍨×¯1+⍳)⊂'⎕SE.UCMD'
         :If 2<10|⎕DR nss
             nss←there{
                 ''≢0⍴⍵:⍵
                 9≠⍺.⎕NC ⍵::⍵
                 ⍺⍎⍵
             }¨nss
         :Else
             nss←there⍎nss
         :EndIf
         linked←⍎¨⎕SE.Link.Links.ns
         broken←0⍴⊂''
         missing←0⍴⊂''
         :For ns :In nss
             mask←linked≠ns
             :If 0∊mask
                 ⎕SE.Link.Links/⍨←mask
                 broken,←⊂ns
             :Else
                 missing,←⊂ns
             :EndIf
         :EndFor
         msg←0⍴⊂''
         :If ×≢broken
             msg,←⊂'Unlinked:',U.List broken
         :EndIf
         :If ×≢missing
             msg,←⊂'Not found:',U.List missing
         :EndIf
         msg←⊃{⍺,'; ',⍵}/msg
     :Else
         msg←'No active links'
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
